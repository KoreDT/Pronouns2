plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.8"
}

configurations {
    shadowCommon
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io/"
    }
    maven {
        url "https://repo.opencollab.dev/maven-releases/"
    }
    maven {
        url "https://repo.opencollab.dev/maven-snapshots/"
    }
    maven {
        url "https://storehouse.okaeri.eu/repository/maven-public/"
    }
}

dependencies {
    compileOnly("org.allaymc.allay:api:master-SNAPSHOT")

    implementation(shadowCommon(project(path: ":common")))
    implementation(shadowCommon(project(path: ":API")))
}

tasks.build.dependsOn tasks.shadowJar

shadowJar {
    configurations = [project.configurations.shadowCommon]
    setArchiveClassifier("")
    destinationDirectory = file("$rootProject.rootDir/output")
}

processResources {
    def props = [
            version: version,
            name: rootProject.modin_version,
            description: rootProject.modin_description,
            website: rootProject.modin_website
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.json') {
        expand props
    }
}