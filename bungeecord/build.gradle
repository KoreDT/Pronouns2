plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.8"
}

repositories {
    mavenCentral()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "minecraft"
        url "https://libraries.minecraft.net"
    }
}

configurations {
    shadowCommon
}

dependencies {
    compileOnly("net.md-5:bungeecord-api:1.21-R0.1-SNAPSHOT")

    implementation(shadowCommon("net.kyori:adventure-platform-bungeecord:4.3.3"))
    implementation(shadowCommon("net.kyori:adventure-text-minimessage:4.17.0"))

    implementation(shadowCommon(project(path: ":common")))
    implementation(shadowCommon(project(path: ":API")))
}

tasks.build.dependsOn tasks.shadowJar

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = null
    destinationDirectory = file("$rootProject.rootDir/output")
}

processResources {
    def props = [version: version, name: rootProject.modin_name, description: rootProject.modin_description]
    inputs.properties props
    expand props
    filteringCharset 'UTF-8'
    filesMatching('bungee.yml') {
        expand props
    }
}

def vt = "release"
if (version.toString().contains("SNAPSHOT")) {
    vt = "beta"
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "kore-pronouns"
    versionNumber = version
    versionType = vt
    uploadFile = shadowJar
    gameVersions = [rootProject.minecraft_version]
    loaders = ["bungeecord", "waterfall"]
    dependencies {

    }
}